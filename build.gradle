// First, apply the publishing plugin
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.2"
    }
}

apply plugin: "com.gradle.plugin-publish"
description = 'gradle-scoverage is a Gradle plugin for calculating code coverage using Scoverage'
if (project.version == 'unspecified') {
    version = '2.0.0-SNAPSHOT'
}

repositories {
    mavenCentral()
}

ext {
    website = 'http://scoverage.org'
    vcsUrl = 'https://github.com/scoverage/gradle-scoverage.git'
    scmUrl = "scm:git:$vcsUrl"
    sonatypeUser = System.env.SONATYPE_USER
    sonatypePass = System.env.SONATYPE_PASS
}

apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'groovy'

group 'org.scoverage'
sourceCompatibility = '1.6'
targetCompatibility = '1.6'

configurations {
    scoverage
    compile.extendsFrom scoverage
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    scoverage 'org.scoverage:scalac-scoverage-plugin_2.11:1.0.2'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from "$buildDir/docs/groovydoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

test {
    dependsOn jar
}

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}

if (project.properties.containsKey('signing.keyId')) {
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}

pluginBundle {
    website = project.website
    vcsUrl = project.vcsUrl
    description = project.description
    tags = ['coverage', 'scala', 'scoverage']

    plugins {
        scoveragePlugin {
            id = 'org.scoverage'
            displayName = 'Gradle Scoverage plugin'
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.properties.containsKey('signing.keyId')) {
              beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUser, password: sonatypePass)
            }

            pom.project {
                name 'GradleScoverage'
                description project.description
                url project.website

                scm {
                    url scmUrl
                    developerConnection scmUrl
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'maiflai'
                    }
                    developer {
                        id 'ubourdon'
                    }
                    developer {
                        id 'D-Roch'
                    }
                }
            }
        }
    }
}
