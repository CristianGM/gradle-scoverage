plugins {
    id 'org.scoverage'
}

description = 'a multi-project with separate tests setup for gradle-scoverage'

allprojects {

    repositories {
        jcenter()
    }

    apply plugin: 'org.scoverage'

    dependencies {
        scoverage "org.scoverage:scalac-scoverage-plugin_2.11:${scoverageVersion}",
                "org.scoverage:scalac-scoverage-runtime_2.11:${scoverageVersion}"
    }
}

subprojects {

    apply plugin: 'scala'

    dependencies {
        compile 'org.scala-lang:scala-library:2.11.4'

        testCompile 'junit:junit:4.11'
    }

    testScoverage {
        onlyIf { project.name.endsWith('-tests') }
    }

    reportScoverage {
        onlyIf { project.name.endsWith('-tests') }
    }

    checkScoverage {
        onlyIf { project.name.endsWith('-tests') }
    }

}

configure(subprojects.findAll { it.name.endsWith('-tests') }) {
    def mainProject = project(":${project.name.minus('-tests')}")
    dependencies {
        testCompile mainProject
        scoverage mainProject.configurations.scoverage.artifacts.files
    }
    scoverage {
        sources = mainProject.extensions.scoverage.sources
        dataDir = mainProject.extensions.scoverage.dataDir
        reportDir = mainProject.extensions.scoverage.reportDir
    }
    sourceSets {
        testScoverage {
            compileClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.main.output
        }
    }
    compileScoverageScala {
        onlyIf { false }
    }
}